plugins {
    id 'org.hidetake.swagger.generator' version '2.11.0'
    id "org.sonarqube" version "2.6.2"
    id "au.com.dius.pact" version "3.2.11"
    id 'com.github.psxpaul.execfork' version '0.1.7'
}
apply plugin: 'java'
apply plugin: 'pmd'
apply plugin: 'jacoco'
//apply plugin: 'findbugs'
apply plugin: 'groovy'

group = 'com.consort'
version = '1.0-SNAPSHOT'

description = """"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}
dependencies {
    compile group: 'com.sparkjava', name: 'spark-core', version:'2.5.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.7'
    compile group: 'com.google.guava', name: 'guava', version:'24.0-jre'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.3'
    compile group: 'io.github.cdimascio', name: 'java-dotenv', version:'3.0.0'
    compile group: 'io.fabric8', name: 'kubernetes-client', version:'3.1.8'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-simple', version:'1.6.1'
    compile group: 'org.pac4j', name: 'spark-pac4j', version:'2.0.0'
    compile group: 'org.pac4j', name: 'pac4j-core', version:'2.0.0'
    compile group: 'org.pac4j', name: 'pac4j-http', version:'2.0.0'
    compile group: 'org.pac4j', name: 'pac4j-jwt', version:'2.0.0'
    compile group: 'com.auth0', name: 'jwks-rsa', version:'0.3.0'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.10'
    testCompile group: 'org.spockframework', name: 'spock-core', version:'1.0-groovy-2.4'
    testCompile group: 'cglib', name: 'cglib-nodep', version:'2.2'
    testCompile group: 'org.objenesis', name: 'objenesis', version:'2.5.1'
    testCompile group: 'au.com.dius', name: 'pact-jvm-consumer-junit_2.11', version:'3.5.0'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'kubernetes-adapter',
                'Implementation-Version': version,
                'Main-Class': 'com.consort.kubernetesadapter.KubernetesAdapterMain'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    with jar
}

task startDaemon(type: com.github.psxpaul.task.JavaExecFork) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.consort.kubernetesadapter.KubernetesAdapterMain'
    args = [ '-d', '/foo/bar/data', '-v', '-l', '3' ]
    jvmArgs = [ '-Xmx500m', '-Djava.awt.headless=true' ]
    workingDir = "$buildDir/server"
    waitForPort = 8081
    waitForOutput = 'Server - Started'
    environment = ["kubeclienturl": "$kubeclienturl", "jwk_url": "$jwk_url", "jwk_alg": "$jwk_alg", "kubeclientbeareruser": "$kubeclientbeareruser", "kubeclientbearertoken": "$kubeclientbearertoken", "jwk_kid": "$jwk_kid"]
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("testresult/jacoco/html")
    }
}

test {
    reports.junitXml.destination = file("build/reports/testresult/xml")
    reports.html.destination = file("build/reports/testresult/html")
    jacoco {
        append = false
        destinationFile = file("build/reports/testresult/jacoco/jacocoTest.exec")
    }
}

pact {
    serviceProviders {
        "kubernetes-adapter" {
            protocol = 'http'
            host = 'localhost'
            port = 8080
            path = '/'

            hasPactWith("cme-ui") {
                pactFile = file('src/test/resources/pacts/cme-ui-kubernetes-adapter.json')
            }
        }
    }
    reports {
      defaultReports() // adds the standard console output

//      markdown // report in markdown format
      json // report in json format
    }
}

def env = System.getenv();
def getDate() {
    def date = new Date()
    return date.format('yyyyMMddHHmmss')
}
sonarqube{
    properties {
        property "sonar.host.url", env.getOrDefault( "SONAR_HOST_URL", "https://cme.dev.k8s.consort-it.de/sonar/")
        property "sonar.login", env.getOrDefault('SONAR_HOST_TOKEN', "")
        property "sonar.projectVersion", getDate()
    }
}

swaggerSources {
    kubernetesadapter {
        inputFile = file('swagger.yaml')
    }
}

build.dependsOn(fatJar)

check.dependsOn(jacocoTestReport)
check.dependsOn(test)
check.dependsOn(validateSwagger)
//check.dependsOn(pactVerify)
project.tasks["sonarqube"].dependsOn "check"


